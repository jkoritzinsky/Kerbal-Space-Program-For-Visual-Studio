@namespace KSP4VS.ConfigNode
@classname NodeParser
@start nodes

whitespace = "" [ \t]+
_ = whitespace?
eq =  "=" 
bro =  "{" 
brc  =  "}"
newline = "\r\n" / "\n" 

comment -lexical =  "//" [^\r\n]* ("\r\n" / "\n")?  
newlineOrComment = "" _ (newline / comment)+

string -lexical = "" [^\r\n=]+
bool -lexical = [Tt]"rue" / [Ff]"alse" &newlineOrComment
number -lexical = '-'? [0-9]+ ('.' [0-9]+)?
numberListComma -lexical = "" (number) ("" _ "," _ number)*
numberListSpace -lexical ="" (number !( _ "," _)) ("" whitespace !(",") _ number)*
numberList -lexical ="" (numberListSpace / numberListComma) &newlineOrComment


name -lexical =  "" [A-Za-z_]+ 
name_InvalidChar -lexical = "" ([^/{} \t\r\n=])+

topOp = "" [@+$%!-]
MMnamePattern = "" [A-Za-z0-9?*_-]+
MMnamePattern_InvalidChar -lexical = "" [^\]]+
nameSelector = "[" ((MMnamePattern ("," MMnamePattern)?) / MMnamePattern_InvalidChar)?  "]"
MMname -lexical = topOp name
MMselectWithOp = MMname ordering? nameSelector?
MMselectWithoutOp = name ordering? nameSelector
ordering = "," "-"? [0-9]+

hasOp = [@!#~]
hasBody = (hasOp MMselectWithoutOp hasExpression*) / MMnamePattern_InvalidChar
hasDeclaration -lexical = "HAS"
hasExpression = ":" hasDeclaration "[" hasBody "]"

needsDeclaration -lexical = "NEEDS"
needsExpression = ":" needsDeclaration "[" (( "!"? MMnamePattern (("&" / "|" / ",") "!"? MMnamePattern)* ) / MMnamePattern_InvalidChar ) "]"

flexOrderDeclaration -lexical = "BEFORE" / "FOR" / "AFTER"
staticOrderDeclaration -lexical = "FINAL" / "FIRST"
orderSelector = "[" ((MMnamePattern ("," MMnamePattern)*) / MMnamePattern_InvalidChar)?  "]"
orderSpecifiers = ":" ((flexOrderDeclaration orderSelector) / staticOrderDeclaration)

MMnodeSelector = (MMselectWithOp / MMselectWithoutOp) (hasExpression/orderSpecifiers/needsExpression)*

valueName -lexical = ((name needsExpression*) / name_InvalidChar) name_InvalidChar?
nodeName -lexical = (MMnodeSelector / (name needsExpression*) / name_InvalidChar) name_InvalidChar? 

value =  bool / numberList / string
variable -lexical = "#$" ("/"? (("@" name ordering? nameSelector?) / "..") ("/"(("@" name ordering? nameSelector?) / ".."))* "/")? name ordering? ("[" [0-9]+ ("," .)? "]")? "$" 
variableList -lexical = variable (_ variable)* &newlineOrComment

MMvalue = variableList / bool / numberList / string

mathOp = [+*%^/-]

MMvalueName = !name ((MMselectWithOp needsExpression*) / name_InvalidChar) name_InvalidChar?

MMnameValuePair -lexical = MMvalueName _ mathOp? eq _ MMvalue

nameValuePair -lexical = valueName _ eq _ v:("" value?) 

nameValuePair_noEq -lexical = _ valueName 

nameValuePair_all = _ (MMnameValuePair / nameValuePair / nameValuePair_noEq) newlineOrComment 

configNodeBody = _ (configNode_all  / nameValuePair_all  / (whitespace / newlineOrComment))* _

configNode  -lexical = _ nodeName _ newlineOrComment? _ bro newlineOrComment? _ configNodeBody newlineOrComment? _ brc _ newlineOrComment?

configNode_noClose  -lexical = _ nodeName _ newlineOrComment _ bro configNodeBody _ newlineOrComment?

configNode_all  = configNode / configNode_noClose

nodes  -lexical = configNodeBody 
